ext {
	springCloudVersion = 'Hoxton.SR2'
	grpcSpringBootStarterVersion = '3.4.1'
	grpcVersion = '1.25.0'
	protobufVersion = '3.9.0'
	commonsCodecVersion = '1.13'
	jacksonVersion = '2.10.0'
	swaggerVersion = '2.9.2'
}

buildscript {
	ext {
		springBootVersion = '2.2.4.RELEASE'
		protobufGradlePluginVersion = '0.8.18'
		jibGradlePluginVersion = '2.1.0'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVersion}"
		classpath "com.google.cloud.tools.jib:com.google.cloud.tools.jib.gradle.plugin:${jibGradlePluginVersion}"
	}
}

apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'com.hibuz.blockchain'
version = '0.0.1-SNAPSHOT'

defaultTasks 'bootRun'

allprojects {
	repositories {
		mavenCentral()
		jcenter()
		maven { url "https://repo.spring.io/milestone" }
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'com.google.cloud.tools.jib'

	sourceCompatibility = 1.8

	dependencies {
		implementation('org.springframework.boot:spring-boot-starter')
		implementation "io.grpc:grpc-netty:${grpcVersion}"
		implementation "io.grpc:grpc-protobuf:${grpcVersion}"
		implementation "io.grpc:grpc-stub:${grpcVersion}"

		testImplementation('org.springframework.boot:spring-boot-starter-test')
	}
	sourceSets {
		main {
			java {
				srcDirs 'src/main/java'
			}
		}
	}
	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
}

project(':chain-core') {
	apply plugin: 'com.google.protobuf'

	dependencies {
		implementation "io.github.lognet:grpc-spring-boot-starter:${grpcSpringBootStarterVersion}"
		implementation "commons-codec:commons-codec:${commonsCodecVersion}"
		implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

		testImplementation "io.grpc:grpc-testing:${grpcVersion}"

	}

	jar {
		enabled = true
	}

	protobuf {
		protoc {
			artifact = "com.google.protobuf:protoc:${protobufVersion}"
		}
		plugins {
			grpc {
				artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
			}
		}
		generateProtoTasks {
			all()*.plugins {
				grpc {}
			}
		}
	}
	sourceSets {
		main {
			java {
				srcDirs 'build/generated/source/proto/main/grpc'
				srcDirs 'build/generated/source/proto/main/java'
			}
		}
	}
}

project(':demo-web') {

	dependencies {
		implementation project(':chain-core')
		implementation "org.springframework.boot:spring-boot-starter-web"
		implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
		implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"
	}
}
